@echo off
rem
rem Run rejection tests
rem
rem The rejection tests use the same format as the acceptance tests, but there
rem is no positive go/no go indication for them. Each test should generate a
rem failure, and all you can say is that the test has failed if there were no
rem error.
rem
rem This file is GPC specific.
rem
rem Run the tests
rem
echo Running tests
set List=standard_tests\iso7185prt*.pas
for /f "delims=" %%a in ('dir /b "%List%"') do (

    call testprog standard_tests\%%~na

)
rem
rem Create combined listing file
rem
echo Creating combined listing
echo *******************************************************************************> standard_tests\iso7185prt.lst
echo.>> standard_tests\iso7185prt.lst
echo Listings for run of iso7185prt >> standard_tests\iso7185prt.lst
echo.>> standard_tests\iso7185prt.lst
echo *******************************************************************************>> standard_tests\iso7185prt.lst
set List=standard_tests\iso7185prt*.pas
for /f "delims=" %%a in ('dir /b "%List%"') do (

    echo.>> standard_tests\iso7185prt.lst
    echo Listing for standard_tests\%%~na *************************************>> standard_tests\iso7185prt.lst
    echo.>> standard_tests\iso7185prt.lst
    echo Compile: >> standard_tests\iso7185prt.lst
    echo.>> standard_tests\iso7185prt.lst
    type standard_tests\%%~na.err >> standard_tests\iso7185prt.lst
    echo.>> standard_tests\iso7185prt.lst
    if exist "standard_tests\%%~na.lst" (

        echo Run: >> standard_tests\iso7185prt.lst
        echo.>> standard_tests\iso7185prt.lst
        type standard_tests\%%~na.lst >> standard_tests\iso7185prt.lst

    )
    

)
rem
rem Make a list of files WITHOUT compile errors
rem
echo Creating error accounting listings
grep -l "Errors in program: 0" standard_tests/iso7185prt*.err > standard_tests/iso7185prt.nocerr
rem
rem Make a list of files WITHOUT runtime errors. This is GPC specific.
rem
grep -L "pint:\|\*\*\*" standard_tests/iso7185prt*.lst > standard_tests/iso7185prt.norerr
rem
rem Find files with NO errors either at compile time or runtime. This is done
rem by concatenating the files, sorting and finding duplicate filenames. That
rem is, if the filename list in both the no compile error and no runtime error
rem lists, then no error at all occured on the file and it needs to be looked
rem at.
rem
cat standard_tests/iso7185prt.nocerr standard_tests/iso7185prt.norerr > temp
sort temp | uniq -d > standard_tests/iso7185prt.noerr
rem
rem Add net error listing to end of combined listing as report
rem
echo.>> standard_tests\iso7185prt.lst
echo Tests for which no compile or runtime error was flagged: **********************>> standard_tests\iso7185prt.lst
echo.>> standard_tests\iso7185prt.lst
type standard_tests\iso7185prt.noerr >> standard_tests\iso7185prt.lst
rem
rem Make a listing of dif files to look at. If you are satisfied with each of
rem the prt output runs, then you can copy the .lst file to the .cmp file and
rem get a 0 dif length file. Then this file will show you the files that
rem don't converge. Note DIFFERENT DOES NOT MEAN *** WRONG ***. It simply may
rem mean the error handling has changed. The purpose of diffing the output files
rem is that it allows you to check that simple changes have not broken anything.
rem
echo creating difference list
dir standard_tests\iso7185prt*.dif > standard_tests/iso7185prt.diflst
